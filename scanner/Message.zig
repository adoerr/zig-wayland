const std = @import("std");
const xml = @import("xml");
const util = @import("util.zig");
const Description = @import("Description.zig");
const Arg = @import("Arg.zig");
const InterfaceMap = @import("InterfaceMap.zig");
const Allocator = std.mem.Allocator;

const Message = @This();

name: []const u8,
type: enum { none, destructor } = .none,
since: u32,
deprecated_since: ?u32,
description: ?Description,
args: []const Arg,

pub fn scan(gpa: Allocator, reader: *xml.Reader) !Message {
    var name: ?[]const u8 = null;
    errdefer if (name) |n| gpa.free(n);
    var is_destructor: bool = false;
    var since: u32 = 1;
    var deprecated_since: ?u32 = null;

    for (0..reader.attributeCount()) |i| {
        const attrib = reader.attributeName(i);

        if (std.mem.eql(u8, attrib, "name"))
            name = try reader.attributeValueAlloc(gpa, i)
        else if (std.mem.eql(u8, attrib, "type"))
            is_destructor = std.mem.eql(u8, reader.attributeValueRaw(i), "destructor")
        else if (std.mem.eql(u8, attrib, "since"))
            since = try std.fmt.parseInt(u32, reader.attributeValueRaw(i), 10)
        else if (std.mem.eql(u8, attrib, "deprecated-since"))
            deprecated_since = try std.fmt.parseInt(u32, reader.attributeValueRaw(i), 10)
        else
            continue;
    }

    var description: ?Description = null;
    errdefer if (description) |*desc| desc.deinit(gpa);

    var args = try std.ArrayList(Arg).initCapacity(gpa, 4);
    errdefer {
        for (args.items) |*arg| arg.deinit(gpa);
        args.deinit(gpa);
    }

    while (reader.read()) |node| switch (node) {
        .eof => return error.UnexpectedEof,
        .element_end => {
            const elem = reader.elementName();
            if (!(std.mem.eql(u8, elem, "request") or std.mem.eql(u8, elem, "event")))
                return error.UnexpectedElementEnd;
            break;
        },
        .element_start => {
            const elem = reader.elementName();
            if (std.mem.eql(u8, elem, "description"))
                description = try Description.parse(gpa, reader)
            else if (std.mem.eql(u8, elem, "arg")) {
                var arg = try Arg.parse(gpa, reader);
                errdefer arg.deinit(gpa);
                try args.append(gpa, arg);
            } else return error.UnexpectedElement;
        },
        else => continue,
    } else |err| return err;

    return .{
        .name = name orelse return error.NameNotFound,
        .type = if (is_destructor) .destructor else .none,
        .since = since,
        .deprecated_since = deprecated_since,
        .description = description,
        .args = try args.toOwnedSlice(gpa),
    };
}

pub fn deinit(self: Message, gpa: Allocator) void {
    if (self.description) |desc| desc.deinit(gpa);
    for (self.args) |arg| arg.deinit(gpa);
    gpa.free(self.name);
    gpa.free(self.args);
}

pub fn emitIncomingMessage(
    self: *const Message,
    gpa: Allocator,
    writer: *std.Io.Writer,
    map: *const InterfaceMap,
    interface: []const u8,
    opcode: usize,
) !void {
    const name = try util.snakeToPascal(gpa, self.name);
    defer gpa.free(name);

    const parent_entry = try map.get(interface);

    if (self.description) |d| try d.write(writer, "\t/// ");
    try writer.print("\tpub const {s}Message = struct {{\n", .{name});
    try writer.print("\t\tpub const _name = \"{s}\";\n", .{self.name});
    try writer.print("\t\tpub const _opcode = {d};\n", .{opcode});
    try writer.writeAll("\t\tpub const _signature = \"");
    for (self.args) |arg| try writer.writeByte(switch (arg.type) {
        .int => 'i',
        .uint, .any_object, .any_optional_object, .@"enum" => 'u',
        .fixed => 'f',
        .string, .optional_string => 's',
        .object, .optional_object => 'o',
        .array => 'a',
        .new_id => 'n',
        .fd => 'd',
        .any_new_id => 'g',
    });
    try writer.writeAll("\";\n\n");
    try writer.print("\t\t{s}: {s}.{s},\n", .{ interface, parent_entry.protocol, parent_entry.type_name });
    for (self.args) |arg| {
        if (arg.description) |d|
            try d.write(writer, "\t\t/// ")
        else if (arg.summary) |s|
            try Description.printSummary(s, "\t\t/// ", writer);

        switch (arg.type) {
            .new_id => |ifce| {
                const entry = try map.get(ifce);
                try writer.print("\t\t{s}: {s}.{s},\n", .{ arg.name, entry.protocol, entry.type_name });
            },
            else => {
                try writer.print("\t\t{s}: ", .{arg.name});
                try arg.writeTypeString(gpa, writer, map);
            },
        }
    }
    try writer.writeAll("\t};\n\n");
}

pub fn emitOutgoingMessage(
    self: *const Message,
    gpa: Allocator,
    writer: *std.Io.Writer,
    map: *const InterfaceMap,
    interface: []const u8,
    opcode: usize,
) !void {
    const parent_interface_entry = try map.get(interface);

    const fn_name = try self.fnName(gpa);
    defer gpa.free(fn_name);

    const max_length = self.calculateMaxLength();
    try writer.print("\n\tpub const {s}_message_opcode = {d};\n", .{ self.name, opcode });
    try writer.print("\tpub const {s}_message_length = {d};\n\n", .{ self.name, max_length });

    if (self.description) |d| try d.write(writer, "\t/// ");
    try for (self.args) |arg| switch (arg.type) {
        .new_id => break self.emitConstructor(gpa, writer, map, parent_interface_entry.type_name, fn_name),
        .any_new_id => break self.emitGenericConstructor(gpa, writer, map, parent_interface_entry.type_name, fn_name),
        else => continue,
    } else self.emitNormal(gpa, writer, map, parent_interface_entry.type_name, fn_name);
}

fn emitNormal(
    self: *const Message,
    gpa: Allocator,
    writer: *std.Io.Writer,
    map: *const InterfaceMap,
    parent_interface: []const u8,
    fn_name: []const u8,
) !void {
    const fd_count = count: {
        var count: usize = 0;
        for (self.args) |arg| {
            if (arg.type == .fd) count += 1;
        }
        break :count count;
    };

    try writer.print("\tpub fn {s}(\n", .{fn_name});
    try writer.print("\t\tself: {s},\n", .{parent_interface});
    try writer.writeAll("\t\tconnection: *Connection,\n");
    for (self.args) |arg| try arg.write(gpa, writer, map);
    try writer.writeAll("\t) core.Connection.SendError!void {\n");

    try writer.writeAll("\t\ttry connection.sendMessage(\n");
    try writer.writeAll("\t\t\tself.getId(),\n");
    try writer.print("\t\t\t{s}_message_length,\n", .{self.name});
    try writer.print("\t\t\t{s}_message_opcode,\n", .{self.name});
    try writer.writeAll("\t\t\t.{\n");
    for (self.args) |arg| if (arg.type != .fd) try writer.print("\t\t\t\t{s}_,\n", .{arg.name});
    try writer.writeAll("\t\t\t},\n");
    if (fd_count == 0) try writer.writeAll("\t\t\t&.{},\n") else {
        try writer.writeAll("\t\t\t&.{\n");
        for (self.args) |arg| if (arg.type == .fd) try writer.print("\t\t\t\t{s}_,\n", .{arg.name});
        try writer.writeAll("\t\t\t},\n");
    }
    try writer.writeAll("\t\t);\n");

    try writer.writeAll("\t}\n\n");
}

fn emitConstructor(
    self: *const Message,
    gpa: Allocator,
    writer: *std.Io.Writer,
    map: *const InterfaceMap,
    parent_interface: []const u8,
    fn_name: []const u8,
) !void {
    const fd_count = count: {
        var count: usize = 0;
        for (self.args) |arg| {
            if (arg.type == .fd) count += 1;
        }
        break :count count;
    };

    const return_arg = for (self.args) |arg| {
        if (arg.type == .new_id) break arg;
    } else unreachable;
    const entry = try map.get(return_arg.type.new_id);

    try writer.print("\tpub fn {s}(\n", .{fn_name});
    try writer.print("\t\tself: {s},\n", .{parent_interface});
    try writer.writeAll("\t\tconnection: *Connection,\n");
    for (self.args) |arg| if (arg.type != .new_id) try arg.write(gpa, writer, map);

    try writer.print(
        "\t) (core.Connection.SendError || core.Connection.CreateObjectError)!{s}.{s} {{\n",
        .{ entry.protocol, entry.type_name },
    );

    try writer.print("\t\tconst {s}_ = try connection.createObject({s}.{s});\n", .{
        return_arg.name,
        entry.protocol,
        entry.type_name,
    });

    try writer.writeAll("\t\ttry connection.sendMessage(\n");
    try writer.writeAll("\t\t\tself.getId(),\n");
    try writer.print("\t\t\t{s}_message_length,\n", .{self.name});
    try writer.print("\t\t\t{s}_message_opcode,\n", .{self.name});
    try writer.writeAll("\t\t\t.{\n");
    for (self.args) |arg| if (arg.type != .fd) try writer.print("\t\t\t\t{s}_,\n", .{arg.name});
    try writer.writeAll("\t\t\t},\n");
    if (fd_count == 0) try writer.writeAll("\t\t\t&.{},\n") else {
        try writer.writeAll("\t\t\t&.{\n");
        for (self.args) |arg| if (arg.type == .fd) try writer.print("\t\t\t\t{s}_,\n", .{arg.name});
        try writer.writeAll("\t\t\t},\n");
    }
    try writer.writeAll("\t\t);\n");

    try writer.print("\t\treturn {s}_;\n", .{return_arg.name});

    try writer.writeAll("\t}\n\n");
}

fn emitGenericConstructor(
    self: *const Message,
    gpa: Allocator,
    writer: *std.Io.Writer,
    map: *const InterfaceMap,
    parent_interface: []const u8,
    fn_name: []const u8,
) !void {
    const fd_count = count: {
        var count: usize = 0;
        for (self.args) |arg| {
            if (arg.type == .fd) count += 1;
        }
        break :count count;
    };
    try writer.print("\tpub fn {s}(\n", .{fn_name});
    try writer.print("\t\tself: {s},\n", .{parent_interface});
    try writer.writeAll("\t\tconnection: *Connection,\n");
    try writer.writeAll("\t\tcomptime T: type,\n");
    try writer.writeAll("\t\tversion: T.Version,\n");
    for (self.args) |arg| if (arg.type != .any_new_id)
        try arg.write(gpa, writer, map);
    try writer.writeAll("\t) (core.Connection.SendError || core.Connection.CreateObjectError)!T {\n");

    try writer.writeAll("\t\tconst new_id = try connection.createObject(T);\n");

    try writer.writeAll("\t\ttry connection.sendMessage(\n");
    try writer.writeAll("\t\t\tself.getId(),\n");
    try writer.print("\t\t\t{s}_message_length,\n", .{self.name});
    try writer.print("\t\t\t{s}_message_opcode,\n", .{self.name});
    try writer.writeAll("\t\t\t.{\n");
    for (self.args) |arg| {
        if (arg.type == .any_new_id) {
            try writer.writeAll("\t\t\twire.GenericNewId.init(T, version, new_id.getId()),\n");
        } else if (arg.type != .fd) try writer.print("\t\t\t\t{s}_,\n", .{arg.name});
    }
    try writer.writeAll("\t\t\t},\n");
    if (fd_count == 0) try writer.writeAll("\t\t\t&.{},\n") else {
        try writer.writeAll("\t\t\t&.{\n");
        for (self.args) |arg| if (arg.type == .fd) try writer.print("\t\t\t\t{s}_,\n", .{arg.name});
        try writer.writeAll("\t\t\t},\n");
    }
    try writer.writeAll("\t\t);\n");

    try writer.writeAll("\t\treturn new_id;\n");

    try writer.writeAll("\t}\n\n");
}

fn emitSerialize(
    self: *const Message,
    writer: *std.Io.Writer,
) !void {
    try writer.print("\t\tconst serialized_len = try wire.serializeMessage" ++
        "(&message, self.getId(), {s}_message_opcode, ", .{self.name});

    if (self.args.len > 0) {
        try writer.writeAll(".{\n");
        for (self.args) |arg| switch (arg.type) {
            .fd => {},
            .any_new_id => try writer.writeAll(
                "\t\t\twire.GenericNewId.init(T, version, new_id.getId()),\n",
            ),
            else => try writer.print("\t\t\t{s}_,\n", .{arg.name}),
        };
        try writer.writeAll("\t\t});\n");
    } else try writer.writeAll(".{});\n");
}

fn calculateMaxLength(self: *const Message) usize {
    var length: usize = 8; // Start at size of message header

    for (self.args) |arg| switch (arg.type) {
        // Strings and arrays have an undefined size, so we can only assume the maximum capacity
        // asserted by libwayland
        .array, .string, .optional_string, .any_new_id => return 4096,
        // Fds are not serialized on the wire, but sent via ancillary
        .fd => {},
        // Everything else is serialized as a 32 bit integer
        else => length += 4,
    };

    return length;
}

fn fnName(self: *const Message, gpa: Allocator) ![]const u8 {
    // Message name is a zig idenitfier such as `error` or `type`,
    // and needs to be wrapped with @"..."
    if (isInvalidId(self.name))
        return self.fnNameInvalid(gpa);

    return util.snakeToCamel(gpa, self.name);
}

fn fnNameInvalid(self: *const Message, gpa: Allocator) ![]const u8 {
    var output = try std.ArrayList(u8).initCapacity(gpa, self.name.len + 3);
    output.appendSliceAssumeCapacity("@\"");
    var it = std.mem.tokenizeScalar(u8, self.name, '_');
    const first_tok = it.next().?;
    output.appendSliceAssumeCapacity(first_tok);
    while (it.next()) |tok| {
        output.appendAssumeCapacity(std.ascii.toUpper(tok[0]));
        if (tok.len > 1) output.appendSliceAssumeCapacity(tok[1..]);
    }
    output.appendAssumeCapacity('"');
    return try output.toOwnedSlice(gpa);
}

// Fix issues with std.zig.isValidId.
// We can keep adding special cases to the or chain as they arrive.
fn isInvalidId(name: []const u8) bool {
    return !std.zig.isValidId(name) or
        std.mem.eql(u8, name, "type");
}
